[Kezdőlap](../README.md)

# Bővített Backus–Naur Forma (ABNF)

## RFC 5234 (STD 68) internetes szabvány (kivonat)

Ez a kivonat csak a jelen szabványra hivatkozó PSR vagy más RFC jobb
megértéséhez feltétlenül szükséges részek magyarítását tartalmazza. A teljes angol
szöveg megtalálható az RFC-ket gondozó szabványosító szervezet, az IETF oldalán:
[RFC 5234](https://tools.ietf.org/html/rfc5234).

## 1. Bevezető

Az internetre vonatkozó műszaki specifikációknak gyakran kell olyan formális
szintaxist meghatározni, ami szabadon alkalmazhatja a szerzők által hasznosnak
tartott jelrendszert. Az idő múlásával a
[Backus–Naur Forma (BNF)](https://hu.wikipedia.org/wiki/Backus%E2%80%93Naur-forma)
egy módosított változata, név szerint a Kibővített BNF (ABNF) vált egyre népszerűbbé
az internetes specifikációkban.

Az ABNF szintaxisával több RFC is foglalkozik, kezdve az ARPANET szöveges
üzenet-formátumaival foglalkozó, 1977-ben napvilágot látott
[RFC 733](https://tools.ietf.org/html/rfc733) ajánlással, amit 20 évvel később
az [RFC 2234](https://tools.ietf.org/html/rfc2234) váltott fel, hogy 2008-ban
átadhassa a helyét az [STD 68](http://www.rfc-editor.org/info/std68) néven
[internetes szabványként](http://www.rfc-editor.org/standards) elfogadott jelen
[RFC 5234](https://tools.ietf.org/html/rfc5234) ajánlásnak.

A szabványos BNF és az ABNF között a fő különbség az elnevezési és ismétlődési
szabályok, a vagylagos kifejezések és az értéktartományok ábrázolása területén
tapasztalható.

**Fordítói megjegyzés:** *a [a 4. fejezetben](#4-abnf-által-meghatározott-abnf)
illetve a [B. függelékben](#B-függelék) meghatározott alapvető és más szabálynevek
a könnyebb érthetőség miatt lokalizált formában szerepelnek nemcsak a szabvány
szövegében, de a rá hivatkozó fordításokban is. A szabályok meghatározásánál a
lokalizált név mellett az eredeti is szerepel.*

## 2. Szabály meghatározások

### 2.1. Szabálynevek

A szabály neve egyszerűen önmaga, tehát karakterek sorozata, amely egy betűvel
vagy számmal kezdődik majd betűk, számok és kötőjelek tetszőleges kombinációjával
folytatódik. A szabályok neve nem kis-, és nagybetű érzékeny. A `<szabálynév>`,
`<Szabálynév>`, `<SZABÁLYNÉV>` és `<sZabÁlyNéV>` mind ugyan arra a szabályra hivatkozik.

Az eredeti BNF szintaxistól eltérően a szabályneveket övező csúcsos zárójelek
("<" és ">") nem szükségesek. Azonban a csúcsos zárójelek egy helyen mégis
használhatók az ABNF-szintaxisban, amikor a jelenlétük megkönnyíti a szabálynevek
elegáns, egyértelmű használatát.

### 2.2. A szabályok alakja

A szabályok bal oldalán csak a szabály neve állhat, amelyet az egyenlőségjel jobb
oldalán található leszármaztatási szabállyal (ami egy kifejezés) lehet behelyettesíteni,
a következő séma szerint:

```
  szabálynév = elemek sorvég
```

Ahol a `<szabálynév>` a szabály neve, amelyet egyenlőségjel választ el magától a
szabálytól. Az egyenlőségjel jobb oldalán található `<elemek>` pedig egy vagy
(szóközzel elválasztva) több további szabálynévből vagy terminális értékből álló
kifejezést takarnak, amelyet egy `<sorvég>` (CRLF) zár le.

### 2.3. Végső értékek

Az ABNF-ben az egyes karakterek végső értékét nem negatív egész számok jelképezik.
A terminális értékeket egy vagy több numerikus karakter segítségével lehet
meghatározni, a szóban forgó karakterek kifejezetten jelzett alapvető értelmezésével.
Jelenleg a következő alapok vannak meghatározva:

```
  b =  bináris

  d =  decimális

  x =  hexadecimális
```

Ezért:

```
  KOCSIVISSZA = %d13

  KOCSIVISSZA = %x0D
```

formában adható meg a `KOCSIVISSZA` US-ASCII karakter decimális és hexadecimális
megjelenítése.

A hasonló értékekből összeállított karakterlánc tömör formában a pont ("."), mint
összefűző karakter használatával adható meg, az alábbi módon:

```
  SORVÉG = %d13.10
```

Az ABNF engedélyezi literál<sup>[1]</sup> szövegek direkt megadását, macskakörmökkel
határolva:

```
  parancs  =  "parancs szövege"
```

A literálok nyomtatható karakterek láncolatából épülnek fel.

*[1] Literál: a literálok név és típusjelzés nélküli adatok, melyek a program
szövegében szerepelnek. A literálok típusa nincs a literál mellett feltüntetve,
de a fordítóprogram képes annak meghatározására a literál alakjából
(implicit típusmeghatározás). Forrás: [Sulinet, A C nyelv alapelemei](https://tudasbazis.sulinet.hu/hu/informatika/informatika/informatika-9-12-evfolyam/a-c-nyelv-alapelemei-jelkeszlet-azonositok-konstansok-operatorok/literalok)*

## 3. Operátorok

### 3.1. Összefűzés

A szabálynevek szóközzel elválasztott felsorolásával egy szabály az értékekből
egyszerű, rendezett karakterláncot (szöveget, azaz az összefüggő karakterek
láncolatát) képes létrehozni. Például:

```
  a-betű       =  %x61           ; a

  b-betű       =  %x62           ; b

  motyogás  =  a-betű b-betű a-betű
```

Így a `<motyogás>` nevű szabály megfelel a kisbetűs "aba" karakterláncnak.

### 3.2. Vagylagos kapcsolat

Az alternatív lehetőségeket a ("/") jelek választják el egymástól, ebből adódóan

```
  egyik / másik
```

kifejezés elfogadja `<egyik>` vagy `<másik>` szabályt is.


### 3.3. Növekményes meghatározás

Néha kényelmes lehet az alternatívák listáját részletekben meghatározni. Tehát egy
kiindulási szabály tartalmazhat egy vagy több lehetőséget is, amit egy későbbi
szabállyal újabbakkal lehet bővíteni. Ez különösen hasznos az azonos szabályokból
leszármazó egyébként független specifikációknál, leginkább a paraméterlistáknál
fordulhat elő.

Az ABNF a következő módon teszi lehetővé a növekményes meghatározást:

```
  kiindulási-szabály =/ további-szabályok
```

Így a `szabályok` alábbi meghatározása

```
  szabályok =  alt1 / alt2

  szabályok =/ alt3

  szabályok =/ alt4 / alt5
```

ugyan az, mintha így adtuk volna meg:

```
  szabályok =  alt1 / alt2 / alt3 / alt4 / alt5
```

### 3.4. Értéktartományok

Az alternatív numerikus értékek meghatározhatók tömör formában is, egy kötőjel ("-")
használatával értéktartományba szervezhetők.

Ezért az alábbi:

```
  DECIMÁLIS =  %x30-39
```

kifejezés megegyezik ezzel:

```
  DECIMÁLIS =  "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"
```

Értéktartomány és numerikus értékek felsorolása nem adható meg ugyan abban a
szövegben. Ezért például egy sorvégek közé zárt nyomtatható karakter
specifikációja így fog kinézni:

```
  char-line = %x0D.0A %x20-7E %x0D.0A
```

### 3.5. Szabályok csoportosítása

Az egyszerű zárójelek közé foglalt elemek csoportját egy elemként kell kezelni,
amelynek tartalma szigorúan meghatározott. Ezért

```
  elem (egyik / másik) izé
```

megfelel (elem egyik izé) vagy (elem másik izé) kifejezéseknek is.

```
  elem egyik / másik izé
```

megfelel (elem egyik) vagy (másik izé) kifejezéseknek.

Erősen ajánlott, hogy az elemek csoportosítását alkalmazzuk inkább, minthogy
a puszta alternatívák megfelelő olvasására támaszkodnánk, főleg ha azok összetett
szabályneveket vagy literálokat is tartalmaznak.

Ezért ajánlott, hogy a félreértések elkerülése végett a fenti alak helyett inkább
az alábbit alkalmazzuk:

```
  (elem egyik) / (másik izé)
```

### 3.6. Elemek ismétlődése

Az elemet megelőző csillag ("\*") operátor röviden jelzi az nulla vagy többszöri
ismétlődését. A teljes forma a következő:

```
  <a>*<b>elem
```

ahol a csillaggal elválasztott `<a>` és `<b>` tetszőleges decimális értékek,
amelyek azt fejezik ki, hogy legalább `<a>` és legfeljebb `<b>` alkalommal fordulhat
elő a jelzett elem. Az alapértelmezett értékek a nulla és a végtelen, így
`*<elem>` kifejezés jelenthet bármilyen számot, akár nullát is. Az `1*<elem>`
kifejezés legalább egy, a `3*3<elem>` pontosan három, míg az `1*2<elem>` egy vagy
két előfordulást jelent.

### 3.7. Elemek meghatározott ismétlődése

A következő kifejezés:

```
  <n>elem
```

egyenértékű az alábbival:

```
  <n>*<n>elem
```

Ez az `<elem>` pontosan `<n>` alkalommal való előfordulását jelenti. Ebből kifolyólag
a `2DECIMÁLIS` kétjegyű decimális számot, a `3ANGOLABC` pedig olyan pontosan háromtagú
karakterláncot jelent, amelyben csak az angol ABC kis-, és nagybetűi fordulnak elő.

### 3.8. Opcionális szabály

Az opcionális elemeket szögletes zárójelek közé kell tenni:

```
  [egyik másik]
```

kifejezés egyenértékű ezzel:

```
  *1(egyik másik).
```

### 3.9. Megjegyzések

Az egysoros megjegyzések pontosvesszővel kezdődnek és a sor végig tartanak.

### 3.10. Operátorok kiértékelési sorrendje

The various mechanisms described above have the following precedence,
   from highest (binding tightest) at the top, to lowest (loosest) at
   the bottom:

      Rule name, prose-val, Terminal value

      Comment

      Value range

      Repetition

      Grouping, Optional

      Concatenation

      Alternative

   Use of the alternative operator, freely mixed with concatenations,
   can be confusing.

      Again, it is recommended that the grouping operator be used to
      make explicit concatenation groups.

## 4. ABNF által meghatározott ABNF

Ez a szintaxis a B. függelékben (alább) megadott alapvető szabályokat is használja.

|   Szabálynév  |   Lokalizált név  |       Kifejezés       |
|---------------|-------------------|:---------------------:|
| `rulelist` | `szabálylista` | `1*( szabály / (*megj-vagy-térköz megj-vagy-újsor) )` |
| `rule` | `szabály` | `szabálynév legyen-egyenlő elemek megj-vagy-újsor` |
| `rulename` | `szabálynév` | `ANGOLABC *(ANGOLABC / DECIMÁLIS / "-")` |
| `defined-as` | `legyen-egyenlő` | `*megj-vagy-térköz ("=" / "=/") *megj-vagy-térköz` |
| `elements` | `elemek` | `vagylagos-felsorolás *megj-vagy-térköz` |
| `c-wsp` | `megj-vagy-térköz` | `TÉRKÖZ / (megj-vagy-újsor TÉRKÖZ)` |
| `c-nl` | `megj-vagy-újsor` | `megjegyzés / SORVÉG` |
| `comment` | `megjegyzés` | `";" *(TÉRKÖZ / NYOMTATHATÓ) SORVÉG` |
| `alternation` | `vagylagos-felsorolás` | `felsorolás *(*megj-vagy-térköz "/" *megj-vagy-térköz felsorolás)` |
| `concatenation` | `felsorolás` | `ismétlődés *(1*megj-vagy-térköz ismétlődés)` |
| `repetition` | `ismétlődés` | `[ismétlés] elem` |
| `repeat` | `ismétlés` | `1*DECIMÁLIS / (*DECIMÁLIS "*" *DECIMÁLIS)` |
| `element` | `elem` | `szabálynév / csoport / elhagyható / karakteres-érték / számérték / karakterlánc-érték` |
| `group` | `csoport` | `"(" *megj-vagy-térköz vagylagos-felsorolás *megj-vagy-térköz ")"` |
| `option` | `elhagyható` | `"[" *megj-vagy-térköz vagylagos-felsorolás *megj-vagy-térköz "]"` |
| `char-val` | `karakteres-érték` | `MACSKAKÖRÖM *(%x20-21 / %x23-7E) MACSKAKÖRÖM` |
| `num-val` | `számérték` | `"%" (bináris-érték / decimális-érték / hexadecimális-érték)` |
| `bin-val` | `bináris-érték` | `"b" 1*BIT [ 1*("." 1*BIT) / ("-" 1*BIT) ]` |
| `dec-val` | `decimális-érték` | `"d" 1*DECIMÁLIS [ 1*("." 1*DECIMÁLIS) / ("-" 1*DECIMÁLIS) ]` | |
| `hex-val` | `hexadecimális-érték` | `"x" 1*HEXADECIMÁLIS [ 1*("." 1*HEXADECIMÁLIS) / ("-" 1*HEXADECIMÁLIS) ]` | |
| `prose-val` | `karakterlánc-érték` | `"<" *(%x20-3D / %x3F-7E) ">"` |

## B. Függelék

### B.1. Alapvető szabályok

Az alapvető szabályok a jobb megkülönböztethetőség érdekében következetesen
nagybetűs írásmóddal szerepelnek.

| ABNF jelölés | Lokalizált jelölés |    Leírás    | Hexadecimális kód, vagy ABNF szabály |
|--------------|--------------------|--------------|---------------------------------|
| `ALPHA` | `ANGOLABC` | az angol ABC kis-, és nagybetűi | `%x41-5A / %x61-7A` |
| `BIT` | `BIT` | egybites bináris érték | `0 / 1` |
| `CHAR` | `ASCIIKARAKTER` | a NUL kivételével bármilyen 7-bites US-ASCII karakter | `%x01-7F` |
| `CR` | `KOCSIVISSZA` | kocsi vissza | `%x0D` |
| `CRLF` | `SORVÉG` | internet szabvány sorvég | `%x0D%x0A` |
| `CTL` | `VEZÉRLŐ` | ASCII [vezérlőkarakterek](https://hu.wikipedia.org/wiki/ASCII#Vez%C3%A9rl%C5%91karakterek) | `%x00-1F / %x7F` |
| `DIGIT` | `DECIMÁLIS` | decimális számjegy | `%x30-39` |
| `DQUOTE` | `MACSKAKÖRÖM` | macskaköröm (Double Quote) | `%x22` |
| `HEXDIG` | `HEXADECIMÁLIS` | hexadecimális számjegy | `DIGIT / "A" / "B" / "C" / "D" / "E" / "F"` |
| `HTAB` | `HTAB` | vízszintes tabulátor | `%x09` |
| `LF` | `SOREMELÉS` | soremelés | `%x0A` |
| `LWSP` | `ÜRESSOR` | csak térközt és/vagy sorvéget tartalmazó sor | `*(TÉRKÖZ / SORVÉG TÉRKÖZ)` |
| `OCTET` | `OKTET` | 8 bites adathalmaz | `%x00-FF` |
| `SP` | `SZÓKÖZ` | szóköz | `%x20` |
| `VCHAR` | `NYOMTATHATÓ` | látható/[nyomtatható karakterek](https://hu.wikipedia.org/wiki/ASCII#A_t%C3%B6bbi_karakterhely) | `%x21-7E` |
| `WSP` | `TÉRKÖZ` | térköz karakterek (szóköz és tab) | `SZÓKÖZ / HTAB` |


[Kezdőlap](../README.md)
