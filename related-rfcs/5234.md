[Kezdőlap](../README.md)

# Bővített Backus–Naur Forma (ABNF)

## RFC 5234 (STD 68) internetes szabvány (kivonat)

Ez a kivonat csak a jelen szabványra hivatkozó PSR vagy más RFC jobb
megértéséhez feltétlenül szükséges részek magyarítását tartalmazza. A teljes angol
szöveg megtalálható az RFC-ket gondozó szabványosító szervezet, az IETF oldalán:
[RFC 5234](https://tools.ietf.org/html/rfc5234).

## 1. Bevezető

Az internetre vonatkozó műszaki specifikációknak gyakran kell olyan formális
szintaxist meghatározni, ami szabadon alkalmazhatja a szerzők által hasznosnak
tartott jelrendszert. Az idő múlásával a
[Backus–Naur Forma (BNF)](https://hu.wikipedia.org/wiki/Backus%E2%80%93Naur-forma)
egy módosított változata, név szerint a Kibővített BNF (ABNF) vált egyre népszerűbbé
az internetes specifikációkban.

Az ABNF szintaxisával több RFC is foglalkozik, kezdve az ARPANET szöveges
üzenet-formátumaival foglalkozó, 1977-ben napvilágot látott
[RFC 733](https://tools.ietf.org/html/rfc733) ajánlással, amit 20 évvel később
az [RFC 2234](https://tools.ietf.org/html/rfc2234) váltott fel, hogy 2008-ban
átadhassa a helyét az [STD 68](http://www.rfc-editor.org/info/std68) néven
[internetes szabványként](http://www.rfc-editor.org/standards) elfogadott jelen
[RFC 5234](https://tools.ietf.org/html/rfc5234) ajánlásnak.

A szabványos BNF és az ABNF között a fő különbség az elnevezési és ismétlődési
szabályok, a vagylagos kifejezések és az értéktartományok ábrázolása területén
tapasztalható.

**Fordítói megjegyzés:** *a [a 4. fejezetben](#4-abnf-által-meghatározott-abnf)
illetve a [B. függelékben](#B-függelék) meghatározott alapvető és más szabálynevek
a könnyebb érthetőség miatt lokalizált formában szerepelnek nemcsak a szabvány
szövegében, de a rá hivatkozó fordításokban is. A szabályok meghatározásánál a
lokalizált név mellett az eredeti is szerepel.*

## 2. Szabály meghatározások

### 2.1. Szabálynevek

A szabály neve egyszerűen önmaga, tehát karakterek sorozata, amely egy betűvel
vagy számmal kezdődik majd betűk, számok és kötőjelek tetszőleges kombinációjával
folytatódik. A szabályok neve nem kis-, és nagybetű érzékeny. A <szabálynév>,
<Szabálynév>, <SZABÁLYNÉV> és <sZabÁlyNéV> mind ugyan arra a szabályra hivatkozik.

Az eredeti BNF szintaxistól eltérően a szabályneveket övező csúcsos zárójelek
("<" és ">") nem szükségesek. Azonban a csúcsos zárójeleket egy helyen mégis
használhatók az ABNF-szintaxisban, amikor a jelenlétük megkönnyíti a szabálynevek
elegáns, egyértelmű használatát.

### 2.2. A szabályok alakja

A szabályok bal oldalán csak a szabály neve állhat, amelyet az egyenlőségjel jobb
oldalán található leszármaztatási szabállyal (ami egy kifejezés) lehet behelyettesíteni,
a következő séma szerint:

```
  szabálynév =  elemek crlf
```

ahol a <szabálynév> a szabály neve, <

   where <name> is the name of the rule, <elements> is one or more rule
   names or terminal specifications and <crlf> is the end-of- line
   indicator, carriage return followed by line feed.  The equal sign
   separates the name from the definition of the rule.

### 2.3. Végső értékek

A terminális értékeket
Terminals are specified by one or more numeric characters with the
   base interpretation of those characters indicated explicitly.  The
   following bases are currently defined:

```
  b           =  binary

  d           =  decimal

  x           =  hexadecimal
```

Ezért:

```
  CR          =  %d13

  CR          =  %x0D
```

   respectively specify the decimal and hexadecimal representation of
   [US-ASCII] for carriage return.

   A concatenated string of such values is specified compactly, using a
   period (".") to indicate separation of characters within that value.
   Ezért:

```
  CRLF        =  %d13.10
```

   ABNF permits specifying literal text string directly, enclosed in
   quotation-marks.  Ezért:

```
  command     =  "command string"
```

   Literal text strings are interpreted as a concatenated set of
   printable characters.

## 3. Operátorok


### 3.1. Összefűzés

A rule can define a simple, ordered string of values (i.e., a
   concatenation of contiguous characters) by listing a sequence of rule
   names.  For example:

```
  foo         =  %x61           ; a

  bar         =  %x62           ; b

  mumble      =  foo bar foo
```

So that the rule <mumble> matches the lowercase string "aba".

### 3.2. Vagylagos kapcsolat

Elements separated by forward slash ("/") are alternatives.
   Therefore,

```
  foo / bar
```

will accept <foo> or <bar>.


### 3.3. Növekményes meghatározás

It is sometimes convenient to specify a list of alternatives in
   fragments.  That is, an initial rule may match one or more
   alternatives, with later rule definitions adding to the set of
   alternatives.  This is particularly useful for otherwise independent
   specifications that derive from the same parent ruleset, such as
   often occurs with parameter lists.  ABNF permits this incremental
   definition through the construct:

```
  oldrule     =/ additional-alternatives
```

   So that the ruleset

```
  ruleset     =  alt1 / alt2

  ruleset     =/ alt3

  ruleset     =/ alt4 / alt5
```

is the same as specifying

```
  ruleset     =  alt1 / alt2 / alt3 / alt4 / alt5
```

### 3.4. Értéktartományok

A range of alternative numeric values can be specified compactly,
   using a dash ("-") to indicate the range of alternative values.
   Ezért az alábbi:

```
  DIGIT       =  %x30-39
```

kifejezés megegyezik ezzel:

```
  DIGIT       =  "0" / "1" / "2" / "3" / "4" / "5" / "6" / "7" / "8" / "9"
```

   Concatenated numeric values and numeric value ranges cannot be
   specified in the same string.  A numeric value may use the dotted
   notation for concatenation or it may use the dash notation to specify
   one value range.  Hence, to specify one printable character between
   end-of-line sequences, the specification could be:

```
  char-line = %x0D.0A %x20-7E %x0D.0A
```

### 3.5. Szabályok csoportosítása

Elements enclosed in parentheses are treated as a single element,
   whose contents are STRICTLY ORDERED.   Thus,

```
  elem (foo / bar) blat
```

which matches (elem foo blat) or (elem bar blat).

```
  elem foo / bar blat
```

matches (elem foo) or (bar blat).

Hence it is recommended that instead of the above form, the form:

```
  (elem foo) / (bar blat)
```

   be used.  It will avoid misinterpretation by casual readers.

   The sequence group notation is also used within free text to set off
   an element sequence from the prose.

### 3.6. Elemek ismétlődése

 The operator "*" preceding an element indicates repetition.  The full
   form is:

```
  <a>*<b>element
```

   where <a> and <b> are optional decimal values, indicating at least
   <a> and at most <b> occurrences of the element.

   Default values are 0 and infinity so that *<element> allows any
   number, including zero; 1*<element> requires at least one;
   3*3<element> allows exactly 3; and 1*2<element> allows one or two.

### 3.7. Elemek meghatározott ismétlődése

A rule of the form:

```
  <n>element
```

is equivalent to

```
  <n>*<n>element
```

   That is, exactly <n> occurrences of <element>.  Thus, 2DIGIT is a
   2-digit number, and 3ALPHA is a string of three alphabetic
   characters.

### 3.8. Elhagyható szabály

Square brackets enclose an optional element sequence:

```
  [foo bar]
```

is equivalent to

```
  *1(foo bar).
```

### 3.9. Megjegyzések

A semicolon starts a comment that continues to the end of line.  This
   is a simple way of including useful notes in parallel with the
   specifications.

### 3.10. Operátorok kiértékelési sorrendje

The various mechanisms described above have the following precedence,
   from highest (binding tightest) at the top, to lowest (loosest) at
   the bottom:

      Rule name, prose-val, Terminal value

      Comment

      Value range

      Repetition

      Grouping, Optional

      Concatenation

      Alternative

   Use of the alternative operator, freely mixed with concatenations,
   can be confusing.

      Again, it is recommended that the grouping operator be used to
      make explicit concatenation groups.

## 4. ABNF által meghatározott ABNF

Ez a szintaxis a B. függelékben (alább) megadott alapvető szabályokat is használja.


         rulelist       =  1*( rule / (*c-wsp c-nl) )

         rule           =  rulename defined-as elements c-nl
                                ; continues if next line starts
                                ;  with white space

         rulename       =  ALPHA *(ALPHA / DIGIT / "-")

        defined-as     =  *c-wsp ("=" / "=/") *c-wsp
                                ; basic rules definition and
                                ;  incremental alternatives

         elements       =  alternation *c-wsp

         c-wsp          =  WSP / (c-nl WSP)

         c-nl           =  comment / CRLF
                                ; comment or newline

         comment        =  ";" *(WSP / VCHAR) CRLF

         alternation    =  concatenation
                           *(*c-wsp "/" *c-wsp concatenation)

         concatenation  =  repetition *(1*c-wsp repetition)

         repetition     =  [repeat] element

         repeat         =  1*DIGIT / (*DIGIT "*" *DIGIT)

         element        =  rulename / group / option /
                           char-val / num-val / prose-val

         group          =  "(" *c-wsp alternation *c-wsp ")"

         option         =  "[" *c-wsp alternation *c-wsp "]"

         char-val       =  DQUOTE *(%x20-21 / %x23-7E) DQUOTE
                                ; quoted string of SP and VCHAR
                                ;  without DQUOTE

         num-val        =  "%" (bin-val / dec-val / hex-val)

         bin-val        =  "b" 1*BIT
                           [ 1*("." 1*BIT) / ("-" 1*BIT) ]
                                ; series of concatenated bit values
                                ;  or single ONEOF range

         dec-val        =  "d" 1*DIGIT
                           [ 1*("." 1*DIGIT) / ("-" 1*DIGIT) ]

         hex-val        =  "x" 1*HEXDIG
                           [ 1*("." 1*HEXDIG) / ("-" 1*HEXDIG) ]

         prose-val      =  "<" *(%x20-3D / %x3F-7E) ">"
                                ; bracketed string of SP and VCHAR
                                ;  without angles
                                ; prose description, to be used as
                                ;  last resort


## B. Függelék

   This appendix contains some basic rules that are in common use.
   Basic rules are in uppercase.  Note that these rules are only valid
   for ABNF encoded in 7-bit ASCII or in characters sets that are a
   superset of 7-bit ASCII.

### B.1. Alapvető szabályok

Az alapvető szabályok a jobb megkülönböztethetőség érdekében nagybetűs írásmóddal
szerepelnek.


         ALPHA          =  %x41-5A / %x61-7A   ; A-Z / a-z

         BIT            =  "0" / "1"

         CHAR           =  %x01-7F
                                ; any 7-bit US-ASCII character,
                                ;  excluding NUL


         CR             =  %x0D
                                ; carriage return

         CRLF           =  CR LF
                                ; Internet standard newline

         CTL            =  %x00-1F / %x7F
                                ; controls

         DIGIT          =  %x30-39
                                ; 0-9

         DQUOTE         =  %x22
                                ; " (Double Quote)

         HEXDIG         =  DIGIT / "A" / "B" / "C" / "D" / "E" / "F"

         HTAB           =  %x09
                                ; horizontal tab

         LF             =  %x0A
                                ; linefeed

         LWSP           =  *(WSP / CRLF WSP)
                                ; Use of this linear-white-space rule
                                ;  permits lines containing only white
                                ;  space that are no longer legal in
                                ;  mail headers and have caused
                                ;  interoperability problems in other
                                ;  contexts.
                                ; Do not use when defining mail
                                ;  headers and use with caution in
                                ;  other contexts.

         OCTET          =  %x00-FF
                                ; 8 bits of data

         SP             =  %x20

         VCHAR          =  %x21-7E
                                ; visible (printing) characters

         WSP            =  SP / HTAB
                                ; white space

















Egy Bővített [Backus–Naur Forma](https://hu.wikipedia.org/wiki/Backus%E2%80%93Naur-forma)
vagy ABNF leírás leszármaztatási szabályok halmaza, amelyeket a következő formában
írnak fel:

**szimbólum = kifejezés a szimbólumra**

A szabály bal oldala azt mutatja, mivel/mikkel helyettesíthető a jobb
oldalon álló szimbólum.

Néhány szabály az ABNF értelmezéshez:
* Az elhagyható/opcionális elemek szögletes zárójelben vannak.
* A választást (logikai vagy) a perjel `/` fejezi ki.
* Egy elem ismétlődését az elem előtti csillag ‘*’ karakter jelöli.
* A terminális elemek neve csupa nagybetűből, míg minden másé csupa kisbetűből áll.
* Az elemek csoportosítására normál zárójelek szolgálnak.
* Az egysoros megjegyzéseket pontosvessző `;` jelöli.

**Gyakori terminális értékek**

| ABNF jelölés  | Jelölés a fordításokban |  jelentés       | PCRE RegEx kifejezés  |
|---------------|-------------------------|-----------------|-----------------------|
| ALPHA         | BETŰ                    | csak kis és nagybetű | `[A-Za-z]` |
| BIT           | BINÁRIS                 | bináris szám    | `[0-1]` |
| CHAR          | ASCII                   | 126 ASCII karakter, kivéve a null | `[\x01-\x7F]` |
| CRLF          | ÚJSOR                   | sorvég          | `(*CRLF)` |
| DIGIT         | DECIMÁLIS               | decimális szám  | `[0-9]` |
| HEXDIG        | HEXADECIMÁLIS           | hexadecimális szám | `[0-9A-F]` |
| SP            | SZÓKÖZ                  | szóköz karakter | `[%x20]` |
| WSP           | TÉRKÖZ                  | szóköz és tabulátor karakterek | `%x09 %x20` |


[Kezdőlap](../README.md)
