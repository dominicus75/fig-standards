[Kezdőlap](../README.md)

# Egységes Erőforrás-azonosító (URI)

## RFC 3986. ajánlás (kivonat)

A jelen kivonat csak az RFC-ajánlásra hivatkozó PSR-szabvány jobb megértéséhez feltétlenül
szükséges részek magyarítását tartalmazza. A teljes angol szöveg megtalálható az
RFC-ket gondozó szabványosító szervezet, az IETF oldalán: [RFC 3986](https://tools.ietf.org/html/rfc3986).

#### 1.1.3. URI, URL, és URN

Az URI (Uniform Resource Identifier, egységes erőforrás-azonosító) egy rövid
karaktersorozat, amelyet egy webes erőforrás azonosítására használunk. Az URI az
erőforrást hely, név vagy mindkettő alapján képes azonosítani, ez alapján két
részhalmazra osztható:
* URL (Uniform Resource Locator - Egységes Erőforrás Helymeghatározó),
* URN (Uniform Resource Name - Egységes Erőforrás Név).

Az URL az erőforrás hely alapján történő azonosításán túlmenően magában foglalja
az erőforrás eléréséhez szükséges protokollt is.

Az URN olyan hivatkozási rendszer, amely az erőforrás egyedi azonosítását névterek
segítségével oldja meg az [RFC2141](https://www.ietf.org/rfc/rfc2141.txt)
ajánlása alapján, anélkül, hogy a helyére vagy az elérhetőségére utalna. Ez akkor
is tartós és globálisan egyedi azonosítókat eredményez, ha az erőforrás már megszűnt,
vagy nem elérhető.

Az URN használatára szemléletes példa a könyvek [ISBN-szám](https://hu.wikipedia.org/wiki/ISBN)
szerinti azonosítása (pl. `urn:isbn:978-963-08-4733-9`), illetve az OSZK elektronikus
dokumentumok hosszútávú azonosítására szolgáló [URN:NBN (National Bibliography Number)
rendszere](http://nbn.urn.hu/).

#### 1.2.3. Hierarchikus azonosítók

Az URI szintaxisa hierarchikusan szervezett, a komponensek jelentőség szerint
csökkenő sorrendben követik egymást balról jobbra. Az általános szintaxis perjelet
("/"), kérdőjelet ("?") és kettős keresztet ("#") alkalmaz határoló karakterként
az összetevők elválasztására. Ez amellett, hogy az ismert szintaxis következetes
alkalmazásával segíti az azonosítók olvashatóságát, a hierarchia egységesített
ábrázolásával az elnevezési sémákon keresztül lehetővé teszi rendszer-független,
a hierarchiához viszonyított hivatkozások készítését.

#### 1.3. Szintaxis jelölőrendszer

Ez a specifikáció a Bővített Backus–Naur Forma (ABNF) jelölőrendszer [RFC 5234](5234.md)
számú ajánlásban leírt változatát alkalmazza, lokalizált elnevezésekkel.

### 2. Karakterkészlet

Az URI szintaxisa biztosítja az adatok helyes kódolását az erőforrások azonosítása
érdekében. Ha az URI tartalmaz protokoll összetevőt is, akkor a karakterkódolást
a megadott protokoll határozza meg.

Az URI-t egy ékezet nélküli latin betűkből, arab számokból és néhány írásjelből
álló korlátozott karakterkészlet alkotja. Ennek egy fenntartott részhalmaza tartalmazza
a szintaxisban az URI komponenseit elválasztó határoló karaktereket, míg az egyes
összetevők azonosító adatait a fennmaradó karakterek - beleértve a nem fenntartott
karakterek mellett azokat a fenntartott karaktereket is, amelyek nem töltenek be
komponens-határoló szerepet - határozzák meg.

#### 2.1. URL-kódolás

Ha a fenntartott karakterek egyike kijelölt céljától eltérő szerepben lenne jelen
egy adott URL-ben, helyére egy százalékjel (%) és a karakter ASCII kódja kerül
kétjegyű hexadecimális formátumban. Például a szóköz karakter URL-kódolva így néz
majd ki: "%20".

```
  url-kódolt = "%" HEXADECIMÁLIS HEXADECIMÁLIS
```
Az ASCII karaktertáblában nem szereplő betűk, például az ékezetes magyar magánhangzók
vagy egyes speciális karakterek [URL-kódolása](https://www.w3schools.com/tags/ref_urlencode.asp)
ettől eltérhet (két vagy három kétjegyű hexadecimális szám jelöli őket). A nagy
"Á" betű például így fog mutatni URL-kódolva: "%C3%81", míg a kicsi így: "%C3%A1".

A hexadecimális számokban szereplő, számokat jelölő betűk írásmódja tetszőleges,
nem kis-, és nagybetű érzékeny, így az "A" éppúgy tízet fog jelenteni, mint az "a".
A következetesség érdekében azonban erősen ajánlott a nagybetűs írásmód használata.

#### 2.2. Fenntartott karakterek

Az URI egyes komponenseit az erre a célra fenntartott karakterek választják el
egymástól. Ha ezek kijelölt céljuktól eltérő szerepben is előfordulnak az URI-ben
akkor a 2.1 fejezetben ismertetett módon kódolni kell őket.

```
  fenntartott    = fő-határolók / más-határolók
  fő-határolók   = ":" / "/" / "?" / "#" / "[" / "]" / "@"
  más-határolók  = "!" / "$" / "&" / "'" / "(" / ")" / "*" / "+" / "," / ";" / "="
```

#### 2.3.  Nem fenntartott karakterek

Az URI-ben engedélyezett, de külön meghatározott céllal nem rendelkező karaktereket
nem fenntartottként tartjuk nyilván. E nem fenntartott karakterek közé tartoznak
az ékezet nélküli latin kis-, és nagybetűk, arab számok és a következő írásjelek:
"–" (kötőjel), "_" (aláhúzás), "." (pont), "~" (hullámvonal).

```
  nem-fenntartott = ANGOLABC / DECIMÁLIS / "-" / "_" / "." / "~"
```

#### 2.4. Mikor kódolunk vagy dekódolunk

Normál körülmények között az URI részei csak a létrehozási folyamat alatt lesznek
URL-kódolva (pl. mielőtt a böngészőnk elküldi a begépelt URL-t). Ilyenkor az alkalmazott
implementáció határozza meg, hogy mely fenntartott karakteret kell komponens-határolóként
alkalmazni és melyeket lehet biztonsággal adatként használni. A kész URI mindig URL-kódolt.

Az implementációknak nem szabad egynél többször URL-kódolni vagy dekódolni ugyanazt
a karakterláncot, mert a már dekódolt szöveg ismételt dekódolása félreértelmezéshez
vezet.

### 3. Szintaktikai összetevők

Az általános URI szintaxis az összetevők hierarchikus sorozatából áll. Az elemek
balról jobbra, csökkenő fontossági sorrendben a következők: séma (scheme),
hitelesítés (authority), útvonal (path), lekérdezés (query) és részerőforrás
(fragment).

```
                URI = séma ":" hierarchikus-rész [ "?" lekérdezés ] [ "#" részerőforrás ]

  hierarchikus-rész = "//" hitelesítés hitelesített-útvonal
                        / abszolút-útvonal
                        / relatív-útvonal
                        / üres-útvonal
```

A séma és az útvonal komponens minden esetben szükséges, de az útvonal lehet
üres is (0 karakter). Ha a hitelesítési komponens jelen van az URI-ben, akkor az
útvonalnak vagy üresnek kell lenni, vagy pedig perjellel ("/") kell kezdődnie.
Amikor az URI nem tartalmaz hitelesítési komponenst, akkor az útvonal nem
kezdődhet dupla perjellel ("//").

A következő két példa az URI hierarchikus felépítését szemlélteti:

```
    http://example.com:8042/over/there?name=ferret#nose
     \_/   \______________/\_________/ \_________/ \__/
      |           |            |            |        |
    séma     hitelesítés    útvonal    lekérdezés részerőforrás
      |   _____________________|__
     / \ /                        \
     urn:example:animal:ferret:nose
```

#### 3.1. Séma (scheme)

Minden egyes URI a séma nevével kezdődik, ami a rendszeren belül az azonosítók
hozzárendelésére vonatkozó specifikációra utal. Mint ilyen, az URI szintaxis egy
kiterjeszthető elnevezési rendszer, amelyben minden séma-specifikáció tovább
szűkítheti a szintaxist és a sémák által használt azonosítók szemantikáját.

A sémák neve a karakterek olyan sorozatából áll, melynek első eleme mindig egy
betű, amelyet tetszőleges kombinációban követhetnek további betűk, számok, illetve
pluszjel ("+"), pont (".") és kötőjel ("-"). Habár a sémák neve nem kis-, és nagybetű
érzékeny, a szabályos alak mégis a kisbetűs írásmód, ezért a sémákat leíró
dokumentumokban is így illik írni.

Egy implementációnak ettől függetlenül a kisbetűkkel egyenértékűnek kell elfogadnia
a nagybetűket is a sémák nevében (pl. "HTTP" és "http" alak) a robusztusság kedvéért,
viszont a következetesség érdekében létrehoznia csak kisbetűs séma nevet lehet.

```
  séma = ANGOLABC *( ANGOLABC / DECIMÁLIS / "+" / "-" / "." )
```

#### 3.2. Hitelesítés (authority)

Számos URI-séma tartalmaz egy hierarchikusan felépülő hitelesítési elemet úgy,
hogy az URI fennmaradó része által meghatározott névtér irányítását átruházza
erre (ami viszont tovább delegálhatja a feladatot). Az általános
szintaxis biztosít egy közös eszközt a hitelesítési komponens megkülönböztetésére,
a gazdagép neve vagy IP-címe, és opcionálisan a port és a felhasználói
információ alapján.

A hitelesítési komponens dupla perjellel ("//") kezdődik és a következő perjelig
("/"), kérdőjelig ("?") vagy kettős keresztig ("#"), ezek hiányában az URI
végéig tart.

```
  hitelesítés = [ userinfo "@" ] gazdagép [ ":" port ]
```

#### 3.2.1. Felhasználói információ (userinfo)

A felhasználói információ egy felhasználói névből és opcionálisan egy
rendszer-specifikus információból állhat, amely azt írja le, milyen hitelesítés
után lehet hozzáférni az erőforráshoz. A felhasználói információt - ha jelen van az
URI-ben - egy kukacjel ("@") választja el a gazdagép nevétől.

```
  userinfo = *( nem-fenntartott / url-kodolt / más-határolók / ":" )
```

A **"felhasználó:jelszó"** formátum használata az URI-ben elavult, biztonsági
kockázatot jelent (nyílt csatornán, kódolatlan formában küld kényes adatot),
ezért kerülendő!

#### 3.2.2. Gazdagép (host)

A hitelesítés mező gazdagép részkomponensét jelölheti egy szögletes zárójelek közé
tett [IP-literál](https://hu.wikipedia.org/wiki/IPv6-c%C3%ADm), vagy a hagyományos,
pontokkal elválasztott négy darab decimális oktettből álló
[IPv4-cím](https://hu.wikipedia.org/wiki/IP-c%C3%ADm), illetve a gazdagép regisztrált
doménneve.

A gazdagép részkomponens nem kis-, és nagybetű érzékeny. A doménnév szintaxisa
megengedi az URL-kódolt tripletek alkalmazását a nem ASCII karaktereket is tartalmazó
nevek mögöttes névfeloldó technológiától független, egységes ábrázolása érdekében.
A nem ASCII karaktereket először az UTF-8 kódtáblának megfelelően kell kódolni,
majd az így nyert UTF-8 karaktersorozat minden egyes oktettét URL-kódolással kell
ellátni, hogy URI-karakterként jelenhessen meg.

```
gazdagép         = IP-literál / IPv4cím / doménnév

IP-literál       = "[" ( IPv6cím / IPvEljövendő  ) "]"

IPvEljövendő     = "v" 1*HEXADECIMÁLIS "." 1*( nem-fenntartott / más-határolók / ":" )

doménnév         = *( nem-fenntartott / url-kódolt / más-határolók )

IPv4cím          = decimális-oktett "." decimális-oktett "." decimális-oktett "." decimális-oktett

decimális-oktett = 1*3DECIMÁLIS  ; 0-255

```

A gazdagép nevére vonatkozó szabályok nem tesznek egyértelmű különbséget az IPv4-cím
és a doménnév között. Az egyértelmű szintaxis érdekében az *"első-találat-nyer"*
algoritmust kell alkalmazni: ha a gazdagép neve megfelel az IPv4-címre vonatkozó
szabályoknak, akkor IPv4-literálnak kell tekinteni és nem doménnévnek.
Noha a gazdagép neve nem kis-, és nagybetű érzékeny, az egységesség
érdekében ajánlott a doménneveket és a hexadecimális címeket kisbetűvel írni és
a nagybetűs írásmódot kizárólag az URL-kódolt tripletekben alkalmazni.

A szögletes zárójelek között megadott IPv6-literállal azonosított gazdagépnév
nem tartalmazhat az IP-cím verziójára utaló előtagot ("v"), mivel maga a szögletes
zárójel határozza meg a cím típusát (ui. ez az egyetlen hely, ahol a szögletes
zárójelek megengedettek az URI szintaxisában).

Egy 128 bites IPv6 címet 8 darab kettősponttal elválasztott, maximum 4 számjegyből
álló, nem kis-, és nagybetű érzékeny hexadecimális szám (a vezető nullák megengedettek)
formájában szokás megjeleníteni, ahol a tagok egyenként 16 bitet vagyis két oktettet
ábrázolnak. Kettő vagy több egymást követő, csak nullákból álló csoportot le
lehet egyszerűsíteni egy üres csoportra két egymást követő kettősponttal (::).
Ezt az egyszerűsítést egy címben csak egyszer lehet elvégezni.

```
IPv6cím          =                            6( h16 ":" ) ls32
                 /                       "::" 5( h16 ":" ) ls32
                 / [               h16 ] "::" 4( h16 ":" ) ls32
                 / [ *1( h16 ":" ) h16 ] "::" 3( h16 ":" ) ls32
                 / [ *2( h16 ":" ) h16 ] "::" 2( h16 ":" ) ls32
                 / [ *3( h16 ":" ) h16 ] "::"    h16 ":"   ls32
                 / [ *4( h16 ":" ) h16 ] "::"              ls32
                 / [ *5( h16 ":" ) h16 ] "::"              h16
                 / [ *6( h16 ":" ) h16 ] "::"

h16              = 1*4HEXADECIMÁLIS
                  ; 16 bites cím, hexadecimális számmal jelölve
ls32             = ( h16 ":" h16 ) / IPv4cím
                  ; legalább 32 bites cím

```

#### 3.2.3. Port

A hitelesítési adatok port részkomponense által kijelölt, 0 és 65535 közötti
tetszőleges decimális portszám a gazdagép neve után áll, attól kettősponttal (":")
elválasztva.

```
  port = *DECIMÁLIS
```

Minden URI-séma meghatározhat egy [alapértelmezett portot](https://hu.wikipedia.org/wiki/TCP_%C3%A9s_UDP_portsz%C3%A1mok_list%C3%A1ja).
A "http" sémának például a 80-as az alapértelmezett portja, ami megfelel a számára
fenntartott TCP portszámnak. A portszám által kijelölt port típusát az URI-séma
határozza meg (TCP, UDP, SCTP). A port komponens és határolókaraktere elhagyható,
ha a portszám üres vagy az értéke megegyezik az URI-séma alapértelmezett portszámával.

### 3.3. Útvonal (path)

Az útvonal komponens általában hierarchikus formában szervezett adatokat tartalmaz,
melyek a nem hierarchikus lekérdezés komponens adataival együtt szolgálnak az
erőforrás azonosítására az URI-séma és az elnevezési felhatalmazás (ha van ilyen)
hatókörén belül. Az útvonal komponens az első kérdőjelig ("?") vagy kettős
keresztig ("#"), ezek hiányában az URI végéig tart.

Ha az URI tartalmaz hitelesítési komponenst, akkor az útvonalnak vagy üresnek
kell lenni, vagy pedig perjellel ("/") kell kezdődnie. Amikor az URI nem tartalmaz
hitelesítési komponenst, akkor az útvonal nem kezdődhet dupla perjellel ("//").

Az útvonal komponens útvonal szegmensek perjellel határolt sorozata. Mindig meg
kell adni az URI-ben, azonban lehet üres is (0 hosszúságú).

```
  útvonal               = hitelesített-útvonal
                          / abszolút-útvonal
                          / séma-nélküli-útvonal
                          / relatív-útvonal
                          / üres-útvonal

  hitelesített-útvonal  = *( "/" szegmens )
                          ; perjellel ("/") kezdődik vagy üres, ha van hitelesítés

  abszolút-útvonal      = "/" [ szegmens-nn *( "/" szegmens ) ]
                          ; perjellel ("/"), de nem dupla perjellel ("//") kezdődik

  séma-nélküli-útvonal  = szegmens-nn-nkp *( "/" szegmens )
                          ; relatív útvonal, az első szegmensben nem lehet kettőspont

  relatív-útvonal       = szegmens-nn *( "/" szegmens )
                          ; szegmenssel kezdődik

  üres-útvonal          = 0<nyomtatható-karakter>
                          ; tök üres (0 karakter)

  szegmens              = *nyomtatható-karakter
  szegmens-nn           = 1*nyomtatható-karakter
  szegmens-nn-nkp       = 1*( nem-fenntartott / url-kódolt / más-határolók / "@" )
                          ; kettőspont (":") nélküli, nullánál hosszabb szegmens

  nyomtatható-karakter  = nem-fenntartott / url-kódolt / más-határolók / ":" / "@"

```

### 3.4. Lekérdezés (query)

A lekérdezés komponens nem hierarchikusan szervezett adatokat tartalmaz, amelyek
az útvonal komponens adataival együtt szolgálnak az erőforrás azonosítására az
URI-séma és az elnevezési felhatalmazás (ha van ilyen) hatókörén belül. A lekérdezés
komponens kezdetét az első kérdőjel ("?") jelöli, a végét pedig kettős kereszt ("#"),
ennek hiányában az URI vége.

```
  lekérdezés = *( nyomtatható-karakter / "/" / "?" )
```

A perjel ("/") és a kérdőjel ("?") karakterek ábrázolhatnak adatot is a lekérdezés
komponensben.

Ezt komponenst gyakran használják azonosítási információk
továbbítására is "kulcs=érték" párok formájában, ahol a más URI-re való hivatkozás
is nagyon gyakori, ami időnként jól használható megoldás ezen karakterek URL-kódolásának
elkerülésére.

### 3.5. Részerőforrás (fragment)

Az URI részerőforrás azonosító komponense lehetővé teszi a másodlagos erőforrások
közvetett azonosítását, amelyek az elsődleges erőforrásra hivatkoznak. Az így
azonosított másodlagos erőforrás jelentheti az elsődleges erőforrás valamely részét
vagy részhalmazát. A részerőforrás azonosítót (nevezik "nevesített horgonynak" is)
a böngészők nem szokták elküldeni a szervernek az URI többi részével, mivel általában
az elsődleges erőforráson belül hivatkozik egy horgonnyal (`anchor`) kijelölt
megjelenítési pontra (a html-kódban `<a name="horgony">Megjelenítési pont.</a>`),
ami a böngésző számára hordoz csak információt, konkrétan azt, hogy ettől a ponttól
kell kezdeni a tartalom megjelenítését. A részerőforrás azonosító komponens kezdetét a kettős
kereszt ("#") karakter jelzi, vége egybeesik az URI végével, így nincs külön jelölése.

```
  részerőforrás = *( nyomtatható-karakter / "/" / "?" )
```

A perjel ("/") és a kérdőjel ("?") karakterek ábrázolhatnak adatot is a
részerőforrás azonosítóban.

### 4. Alkalmazás

Amikor az alkalmazások hivatkoznak egy URI-re, nem mindig használják az URI
szintaxis által meghatározott teljes formátumot. Helytakarékosságból és kihasználandó
a hierarchikus helymeghatározás előnyeit, számos internet protokoll elem és
média formátum engedélyezi az URI rövidítését, míg mások az URI egy adott formájára
korlátozzák a szintaxist. Jelen specifikáció a hivatkozásoknál alkalmazott szintaxis
legelterjedtebb formáit határozza meg.

#### 4.1. URI hivatkozás

Az URI-hivatkozás az erőforrás-azonosító legelterjedtebb használatának jelzésére szolgál.

```
  URI-hivatkozás = URI / relatív-hivatkozás
```

Az URI-hivatkozás vagy egy komplett URI vagy egy relatív hivatkozás. Ha az URI-hivatkozás
előtagja nem egyezik meg a kettősponttal határolt séma szintaxisával, avagy a
séma teljesen hiányzik belőle, akkor relatív hivatkozásról beszélünk.

#### 4.2. Relatív hivatkozás

A relatív hivatkozás a hierarchikus szintaxis előnyeit használja ki, amikor egy
URI hivatkozást egy névtérhez vagy egy másik hierarchikus URI-hez viszonyítva
fejez ki.

```
  relatív-hivatkozás = relatív-rész [ "?" lekérdezés ] [ "#" részerőforrás ]

  relatív-rész       = "//" hitelesítés hitelesített-útvonal
                        / abszolút-útvonal
                        / séma-nélküli-útvonal
                        / üres-útvonal
```

A dupla perjellel ("//") kezdődő relatív hivatkozást hálózati útvonal hivatkozásnak
nevezik, és meglehetősen ritkán használják. Egy relatív hivatkozás, amely egy perjel
("/") karakterrel kezdődik, abszolút elérési utat jelöl, míg amelyik nem, azt
nevezzük relatív útvonal hivatkozásnak.

A kettőspontot is tartalmazó útvonal szegmens (pl. "ez:az") nem használható
relatív útvonal hivatkozás első szegmenseként, mert összetéveszthető lenne a
séma nevével. Ezt elkerülendő, az ilyen szegmenst ki kell egészíteni egy
pontszegmenssel (pl. "./ez:az").

#### 4.3. Abszolút URI

Egyes protokoll-elemek csak az URI egy részerőforrás-azonosító nélküli abszolút
formáját engedélyezik. Például egy relatív hivatkozásokkal való későbbi használat
céljából meghatározott bázis URI abszolút-URI szintaxis szabályt igényel, ami
viszont nem engedélyezi a részerőforrás-azonosítót.

```
  abszolút-URI  = séma ":" hierarchikus-rész [ "?" lekérdezés ]
```

### 7. Biztonsági megfontolások

Az URI önmagában nem jelent biztonsági fenyegetést. Mindamellett gyakran
használatos hálózati erőforrásokhoz való hozzáférésre vonatkozó kompakt utasítások
biztosítására, ezért ügyelni kell az URI adatainak megfelelő értelmezésére.
Meg kell előzni az adatok nem kívánt elérést, illetve az olyan adatok
beillesztését, amelyek nem jeleníthetők meg egyszerű szövegként.

#### 7.2. Rosszindulatú konstrukciók

Időnként lehetséges olyan URI szerkesztése, amely látszólag egy ártalmatlan,
[idempotens művelettel](https://hu.wikipedia.org/wiki/HTTP#Idempotens_met%C3%B3dusok)
próbálkozik, mint egy reprezentáció visszakeresése, valójában egy esetlegesen káros
távoli működést idézhet elő. Nem biztonságos URI leginkább olyan portszám megadásával
állítható elő, amely nem az adott protokoll számára van fenntartva.

Amikor egy URI olyan URL-kódolt oktetteket tartalmaz, amik megfelelnek egy adott
felbontás vagy dereferencia protokoll határolóinak (pl. CR és LF karakterek a
Telnet protokollban), ezeket nem szabad dekódolni a protokoll szerinti átvitel
előtt. Az URL-kódolt adat átadása, ami ugyan sértheti a protokollt, kevésbé káros, mint
megengedni a dekódolt oktettek további műveletekként vagy paraméterekként való
kiértékelését, ami nem kívánt és potenciálisan veszélyes távoli műveleteket válthat ki.

#### 7.5. Érzékeny adatok

Az URI nem tartalmazhat titkosnak szánt adatokat, különös tekintettel a
felhasználónévre és a jelszóra. Az URI ugyanis gyakran jelenik meg böngészőkben,
így egyszerű szövegként kerül eltárolásra az előzményekben és a könyvjelzők
között is, ezen felül a kliens programok és közvetítő alkalmazások
(pl. [proxyk](https://hu.wikipedia.org/wiki/Proxy_szerver)) még naplózzák is.
A jelszó megjelenítése a felhasználói információkat tartalmazó URI-komponensben
elavult és ezért hibának kell tekinteni (vagy egyszerűen figyelmen
kívül hagyni).

#### 7.6. Szemantikus támadások

Mivel a hitelesítési komponensben a gazdagép neve előtt megjelenő felhasználói
információ (userinfo) részkomponenst ritkán használják, ezért felhasználható
olyan URI megszerkesztésére, melynek célja, hogy félrevezesse a gyanútlan
felhasználót. Neki úgy tűnik, hogy az URI egy megbízható tartalomszolgáltatót azonosít,
miközben egy másik webhely ip-címe bújik meg a kukac mögött:

```
  ftp://cnn.example.com&story=breaking_news@10.0.0.1/top_story.htm
```

A fenti példa URI azt eredményezheti, hogy a felhasználó úgy véli, a gazdagép neve
`cnn.example.com`, noha ténylegesen a `10.0.0.1` ip-címre mutat a hivatkozás.
Megjegyzendő, hogy bizonyos esetekben a megtévesztő felhasználói információ
komponens jóval hosszabb is lehet, mint a példánkban szereplő.

A fenti példában is látott megtévesztő hivatkozás a felhasználó URI-ről
kialakított elképzeléseit használja ki és ezekre alapítja a támadást, ahelyett,
hogy a szoftvert magát támadná. A kliensprogramok csökkenhetik az ilyen támadások
hatásait azzal, hogy megfelelően felismerik az URI különböző komponenseit és
különböző színeket vagy árnyalatokat használnak a felhasználói adatok megjelenítésére,
ha van ilyen az URI-ben. Ez sem csodaszer, de a semminél azért több.


[Kezdőlap](../README.md)
